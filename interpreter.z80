	OUTPUT	neo48.rom
	SIZE	4000H

	MACRO PADTO addr ; z80asm "FORG" replacement
		; add padding + display warning
		IF $ > addr
			; no padding
			DISPLAY /L, "Warning! PADTO failed! ", $, " is more than ", addr
		ELSE
			; add padding
			BLOCK addr-$
		ENDIF
		ORG	addr
	ENDM

	DEFINE	R_RESET	$00
	DEFINE	R_1	$08
	DEFINE	R_2	$10
	DEFINE	R_3	$18
	DEFINE	R_4	$20
	DEFINE	R_5	$28
	DEFINE	R_ERR	$30
	DEFINE	R_MATH	$38

	DEFINE	ERR_INT_OPCODE	$01
	DEFINE	ERR_INT_INVALID	$02
	DEFINE	ERR_RUN_OVERFL	$11

; Alternativelyâ€¦ use AF' (flags) to store previous boolean result.

	PADTO	$0080
interpreter:
	ld	b, (TABLE0_LUT >> 8)
	ex	de, hl
	jp	interpret_next

	MACRO	INTERPRET_NEXT
	; Main fetch/decode part is 8 bytes, 46 tstates
	ld	a, (de)
	inc	de
	ld	h, (TABLE0_LUT >> 8)	; Holds MSB of opcode LUT, and interpreter code is at b * 2
	ld	l, a
	ld	l, (hl)
	add	hl, hl  ; If we could squeeze all main interpreter code into 256 bytes, we could change to "inc h" and save 7 t-states
	jp	(hl)
	ENDM

interpret_next_inc1:
	inc	de
interpret_next:
	INTERPRET_NEXT

; Main fetch/decode part is 9 bytes, 39 tstates
interpret_false:
	ld	a, (de)
	inc	de
	ld	b, a
	and	$f8
	ld	h, FALSE_OPS>>8
	ld	l, a
	jp	(hl)

; Main fetch/decode part is 9 bytes, 39 tstates
interpret_true:
	ld	a, (de)
	inc	de
	ld	b, a
	and	$f8
	ld	h, TRUE_OPS>>8
	ld	l, a
	jp	(hl)

;------------------------------------------------

	DEFINE	TABLE0_OPS	$200
	MACRO TABLE0 code
		IF (code - TABLE0_OPS) % 2 != 0
			DISPLAY /L, "Warning! Opcode routine ", $ - TABLE0_LUT, " is not aligned."
		ENDIF
		DB	(code - TABLE0_OPS) / 2
	ENDM
	MACRO TABLE0x16 code
		IF (code - TABLE0_OPS) % 2 != 0
			DISPLAY /L, "Warning! Opcode routine ", $ - TABLE0_LUT, " is not aligned."
		ENDIF
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
		DB	(code - TABLE0_OPS) / 2
	ENDM
	MACRO TABLE0_AT opcode_offset
		BLOCK	opcode_offset - ($ - TABLE0_LUT)
	ENDM


	PADTO	$0100
TABLE0_LUT:
	TABLE0_AT $00
	TABLE0(statement_nop)	; 00 NOP
	TABLE0(statement_nop)	; 01 line-continuation
	TABLE0(statement_nop)	; 02 end-if
	TABLE0(statement_nop)	; 03 else
	TABLE0(statement_nop)	; 04 parens

	TABLE0_AT $0E
	TABLE0(statement_halt)

	TABLE0_AT $12
	TABLE0(literal_false_immediate)	; 12
	TABLE0(literal_true_immediate)	; 13
	TABLE0(statement_nop)
	TABLE0(literal_false_stack)	; 15
	TABLE0(literal_true_stack)	; 16

	TABLE0_AT $18
	TABLE0(literal_0int16)	; 18
	TABLE0(literal_0int32)	; 19
	TABLE0(literal_0int32)	; 1A
	TABLE0(literal_1int16)	; 1B
	TABLE0(literal_1int32)	; 1C
	TABLE0(literal_1int32)	; 1D

	TABLE0_AT $20	; 16/32-bit loads (short codes)
	TABLE0(statement_load16_0)
	TABLE0(statement_load32_0)
	TABLE0(statement_load16_1)
	TABLE0(statement_load32_1)
	TABLE0(statement_load16_2)
	TABLE0(statement_load32_2)
	TABLE0(statement_load16_n)
	TABLE0(statement_load32_n)
	TABLE0(statement_load16_n)
	TABLE0(statement_load32_n)
	TABLE0(statement_load16_n)
	TABLE0(statement_load32_n)
	TABLE0(statement_load16_n)
	TABLE0(statement_load32_n)
	TABLE0(statement_load16_n)
	TABLE0(statement_load32_n)

	TABLE0_AT $30
	TABLE0x16(statement_jumpfwd)	; endif

	TABLE0_AT $40
	TABLE0x16(statement_for)	; for

	TABLE0_AT $50
	TABLE0x16(statement_jumpback)	; endloop

	TABLE0_AT $70 ; 2-byte literals
	TABLE0(statement_literalint16)	; 70
	TABLE0(statement_literalint16)	; 71
	TABLE0(statement_literalint16)	; 72


	TABLE0_AT $80 ; int16 operations
	TABLE0(statement_int16_eq)	; 80
	TABLE0(statement_int16_ne)	; 81
	TABLE0(statement_int16_gt)	; 82
	TABLE0(statement_int16_ge)	; 83
	TABLE0(statement_int16_lt)	; 84
	TABLE0(statement_int16_le)	; 85
	TABLE0(statement_int16_add)	; 86
	TABLE0(statement_int16_sub)	; 87
	TABLE0(statement_int16_mul)	; 88
	TABLE0(statement_int16_div)	; 89
	TABLE0(statement_int16_mod)	; 8a
	TABLE0(statement_int16_bitand)	; 8b
	TABLE0(statement_int16_bitor)	; 8c
	TABLE0(statement_int16_bitxor)	; 8d
	TABLE0(statement_int16_bitshl)	; 8e
	TABLE0(statement_int16_bitshr)	; 8f

	TABLE0_AT $90

	TABLE0_AT $A0

	TABLE0_AT $B0

	TABLE0_AT $C0	; 16/32-bit stores (short codes)
	TABLE0(statement_store16_0)
	TABLE0(statement_store32_0)
	TABLE0(statement_store16_1)
	TABLE0(statement_store32_1)
	TABLE0(statement_store16_2)
	TABLE0(statement_store32_2)
	TABLE0(statement_store16_n)
	TABLE0(statement_store32_n)
	TABLE0(statement_store16_n)
	TABLE0(statement_store32_n)
	TABLE0(statement_store16_n)
	TABLE0(statement_store32_n)
	TABLE0(statement_store16_n)
	TABLE0(statement_store32_n)
	TABLE0(statement_store16_n)
	TABLE0(statement_store32_n)

	TABLE0_AT $D0 ; 1-byte literals (and loads+stores)

	TABLE0_AT $E0

	TABLE0_AT $F0

	PADTO	$0200
;TABLE0_OPS:
statement_error:
	RST	R_ERR
	BYTE	ERR_INT_OPCODE

statement_load16_0:
	ALIGN	2
	exx
	push	bc
	exx
	jp	interpret_next

statement_load16_1:
	ALIGN	2
	exx
	push	de
	exx
	jp	interpret_next

statement_load16_2:
	ALIGN	2
	exx
	push	hl
	exx
	jp	interpret_next

	ALIGN	2
statement_load16_3:
	ld	c, (ix+6)
	ld	b, (ix+7)
	push	bc
	jp	interpret_next

	ALIGN	2
statement_load16_n:
	and	$0e
	push	ix
	pop	hl
	ld	c, a
	ld	b, 0
	add	hl, bc
	ld	c, (hl)
	inc	hl
	ld	b, (hl)
	push	bc
	jp	interpret_next

	ALIGN	2
statement_store16_0:
	exx
	pop	bc
	exx
	jp	interpret_next

	ALIGN	2
statement_store16_1:
	pop	bc
	ld	(ix+2), c
	ld	(ix+3), b
	jp	interpret_next

	ALIGN	2
statement_store16_2:
	pop	bc
	ld	(ix+4), c
	ld	(ix+5), b
	jp	interpret_next

	ALIGN	2
statement_store16_3:
	pop	bc
	ld	(ix+6), c
	ld	(ix+7), b
	jp	interpret_next

	ALIGN	2
statement_store16_n:
	and	$07
	ld	(selfmodifying_store16_frame), a
	inc a
	ld	(selfmodifying_store16_frame), a
	jp	selfmodifying_store16

statement_load32_0:
statement_store32_0:
statement_load32_1:
statement_store32_1:
statement_load32_2:
statement_store32_2:
statement_load32_n:
statement_store32_n:

	ALIGN	2
statement_nop:
	jp	interpret_next

	ALIGN	2
literal_false_stack:
	jp	false_push

	ALIGN	2
literal_true_stack:
	jp	true_push

	ALIGN	2
literal_false_immediate:
	jp	interpret_false

	ALIGN	2
literal_true_immediate:
	jp	interpret_true

	ALIGN	2
literal_0int16
	sbc	hl,hl
	push	hl
	jp	interpret_next

	ALIGN	2
literal_1int16
	sbc	hl,hl
	inc	l
	push	hl
	jp	interpret_next

	ALIGN	2
literal_0int32
	sbc	hl,hl
	push	hl
	push	hl
	jp	interpret_next

	ALIGN	2
literal_1int32
	sbc	hl,hl
	push	hl
	inc	l
	push	hl
	jp	interpret_next

	ALIGN	2
statement_int16_eq:
	pop	hl
	pop	bc
	sbc	hl, bc
	jp	nz, interpret_false
	jp	interpret_true

	ALIGN	2
statement_int16_ne:
	pop	hl
	pop	bc
	sbc	hl, bc
	jp	z, interpret_false
	jp	interpret_true

	ALIGN	2
statement_int16_lt:
	pop	bc
	pop	hl
	sbc	hl, bc
	jp	c, interpret_true
	jp	interpret_false

	ALIGN	2
statement_int16_ge:
	pop	bc
	pop	hl
	sbc	hl, bc
	jp	nc, interpret_true
	jp	interpret_false

	ALIGN	2
statement_int16_gt:
	pop	hl
	pop	bc
	sbc	hl, bc
	jp	c, interpret_true
	jp	interpret_false

statement_int16_le:
	pop	hl
	pop	bc
	sbc	hl, bc
	jp	nc, interpret_true
	jp	interpret_false

	ALIGN 2
statement_int16_add:
	pop	hl
	pop	bc
	add	hl, bc
	push	hl
	jp	po, interpret_next
	rst	R_ERR
	BYTE	ERR_RUN_OVERFL

	ALIGN 2
statement_int16_sub:
	pop	bc
	pop	hl
	sbc	hl, bc
	push	hl
	jp	po, interpret_next
	rst	R_ERR
	BYTE	ERR_RUN_OVERFL

	ALIGN 2
statement_literalint16:
	ld	a, (de)
	inc	de
	ld	l, a
	ld	a, (de)
	ld	h, a
	push	hl
	jp	interpret_next_inc1

	ALIGN 2
statement_int16_mul:
statement_int16_div:
statement_int16_mod:

	ALIGN 2
statement_int16_bitand:
	pop	hl
	pop	bc
	ld	a, h
	and	b
	ld	h, a
	ld	a, l
	and	c
	ld	l, a
	push	hl
	jp	interpret_next

	ALIGN 2
statement_int16_bitor:
	pop	hl
	pop	bc
	ld	a, h
	or	b
	ld	h, a
	ld	a, l
	or	c
	ld	l, a
	push	hl
	jp	interpret_next

	ALIGN 2
statement_int16_bitxor:
	pop	hl
	pop	bc
	ld	a, h
	or	b
	ld	h, a
	ld	a, l
	or	c
	ld	l, a
	push	hl
	jp	interpret_next

statement_int16_bitshl:
statement_int16_bitshr:
statement_for:

statement_halt:
	halt

	ALIGN 2
statement_jumpfwd:	; 5 bytes, 22 tstates (excluding common part)
	and	$0f
	jp	common_jump
	ALIGN 2
statement_jumpback:	; 5 bytes, 22 tstates (excluding common part)
	or	$f0
common_jump:	; 4 bytes, 26 tstates
	ex	de, hl
	ld	d, a
	ld	e, (hl)	; do NOT inc de because we immediately jump
	add	hl, de
	ex	de, hl
	; then a copy of "interpret_next"
	INTERPRET_NEXT
;------------------------------------------------
	PADTO	$0400
FALSE_OPS:

false_discard:
	PADTO	FALSE_OPS+$00
	INTERPRET_NEXT

false_parens:
	PADTO	FALSE_OPS+$08
	jp	interpret_false

false_eq:	; 8 bytes, 34 tstates
	PADTO	FALSE_OPS+$10
	pop	af
	or	a
	jp	nz, interpret_false
	jp	interpret_true

false_ne:	; 8 bytes, 34 tstates
	PADTO	FALSE_OPS+$18
	pop	af
	or	a
	jp	nz, interpret_false
	jp	interpret_true

false_not:
	PADTO	FALSE_OPS+$20
	jp	interpret_true

false_shortcircuit_and:
	PADTO	FALSE_OPS+$28
	xor	a
	jp	common_jump

false_shortcircuit_or:
	PADTO	FALSE_OPS+$30
	jp	interpret_next_inc1

false_push:
	PADTO	FALSE_OPS+$50
	ld	l, %11111000
	push	hl
	INTERPRET_NEXT

false_statement_if:	; 7 bytes, 25 tstates
	PADTO	FALSE_OPS+$80
	ld	a, b
	and	$0f
	jp	common_jump

	PADTO	FALSE_OPS+$88
	ld	a, b
	and	$0f
	jp	common_jump

false_statement_elseif:	; 7 bytes, 25 tstates
	PADTO	FALSE_OPS+$90
	ld	a, b
	and	$0f
	jp	common_jump

	PADTO	FALSE_OPS+$98
	ld	a, b
	and	$0f
	jp	common_jump

false_statement_while:	; 7 bytes, 25 tstates
	PADTO	FALSE_OPS+$a0
	ld	a, b
	and	$0f
	jp	common_jump

	PADTO	FALSE_OPS+$a8
	ld	a, b
	and	$0f
	jp	common_jump

false_statement_break:	; 7 bytes, 25 tstates
	PADTO	FALSE_OPS+$b0
	jp	interpret_next_inc1

	PADTO	FALSE_OPS+$b8
	jp	interpret_next_inc1

false_statement_continue:	; 7 bytes, 25 tstates
	PADTO	FALSE_OPS+$c0
	jp	interpret_next_inc1

	PADTO	FALSE_OPS+$c8
	jp	interpret_next_inc1

;------------------------------------------------
	PADTO	$0500
TRUE_OPS:

true_discard:
	PADTO	TRUE_OPS+$00
	INTERPRET_NEXT

true_parens:
	PADTO	TRUE_OPS+$08
	jp	interpret_true

true_eq:	; 7 bytes, 30 tstates
	PADTO	TRUE_OPS+$10
	pop	af
	jp	z, interpret_false
	jp	interpret_true

true_ne:	; 7 bytes, 30 tstates
	PADTO	TRUE_OPS+$18
	pop	af
	jp	nz, interpret_false
	jp	interpret_true

true_not:
	PADTO	TRUE_OPS+$20
	jp	interpret_false

true_shortcircuit_and:
	PADTO	TRUE_OPS+$28
	jp	interpret_next_inc1

true_shortcircuit_or:
	PADTO	TRUE_OPS+$30
	xor	a
	jp	common_jump

true_push:
	PADTO	TRUE_OPS+$50
	ld	l, %00000111
	push	hl
	INTERPRET_NEXT

true_statement_if:	; 7 bytes, 25 tstates
	PADTO	TRUE_OPS+$80
	jp	interpret_next_inc1

	PADTO	TRUE_OPS+$88
	jp	interpret_next_inc1

true_statement_elseif:
	PADTO	TRUE_OPS+$90
	jp	interpret_next_inc1

	PADTO	TRUE_OPS+$98
	jp	interpret_next_inc1

true_statement_while:
	PADTO	TRUE_OPS+$a0
	jp	interpret_next_inc1

	PADTO	TRUE_OPS+$a8
	jp	interpret_next_inc1

true_statement_break:
	PADTO	TRUE_OPS+$b0
	and	$0f
	jp	common_jump

	PADTO	TRUE_OPS+$b8
	and	$0f
	jp	common_jump

true_statement_continue:
	PADTO	TRUE_OPS+$c0
	or	$f0
	jp	common_jump

	PADTO	TRUE_OPS+$c8
	or	$f0
	jp	common_jump

	ORG	$ffe0
selfmodifying_load16:

selfmodifying_store16:
	pop	bc
	ld	(ix+6), c
	ld	(ix+7), b
	jp	interpret_next
selfmodifying_store16_frame:

/*
interpret_with16bitvalue
	ld	a, (de)
	inc	de
	or	a
	jp	pe, interpret_rest
	push	bc
	ld	h, opcodes_new_value
	ld	l, a
	ld	l, (hl)
	ld	h, interpreter_code
	jp	(hl)
interpret_next:
	ld	h, (TABLE0_LUT >> 8)	; Holds MSB of opcode LUT, and interpreter code is at b * 2
	ld	l, a
	ld	l, (hl)
	add	hl, hl  ; If we could squeeze all main interpreter code into 256 bytes, we could change to "inc h" and save 7 t-states
	jp	(hl)
*/