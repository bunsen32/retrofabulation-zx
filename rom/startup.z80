INIT:
SPLASH_INIT:
	ld sp,$5B00
	ld b,(24 * 32) / 8
	ld de,$0000	; No bright, paper = black, ink = black
.cls_black:	push de
	push de
	push de
	push de
	djnz .cls_black

	ld h,3	; 3 screen-thirds (loop counter)
.loop_screen_thirds:
	exx 	; [[START...
	ld de,$0f0f
	exx 	; ...END]]
	ld de,$ffff
	ld l,4	; 4 pattern repeats in screen-third (loop counter)
.loop_lines_in_cell:
	ld a,16	; 16 pairs of identical pixel bytes in screen third (loop counter)
.loop_cells_in_third:
	ld b,32/4	; 4-byte (32-bit) ‘words’ in the pixel line (loop counter)
.loop_words_in_line:
	push de
	push de
	djnz .loop_words_in_line
.next_pixel_row:	exx 	; BALANCED BECAUSE EVEN # LOOP ITERATIONS (16)
	dec a
	jr nz,.loop_cells_in_third
	srl e
	ld d,e
	exx 	; [[START...
	srl e
	ld d,e
	exx 	; ...END]]
	dec l
	jr nz,.loop_lines_in_cell
	dec h
	jr nz,.loop_screen_thirds

GLOBALS_INIT:
	ld hl,INITIAL_GLOBAL_VALUES
	ld de,G.need_init
	ld bc,INITIAL_GLOBAL_VALUES.length
	ldir	; After this, BC = 0

	ld hl,$5fff	; Last valid byte of RAM (tested so far).
	ld sp,hl	; Placeholder stack top. We’ll move it to the correct place after we’ve tested the RAM.
	call RESET_UDG
	ei

SPLASH_ANIM:
	ld iy,.ram_test_return
	exx	; HL' = end of RAM; IY = RAM test return address; C' = 0 (will be size of RAM in KB); B' = 0 (will be OR of keypresses during RAM test)
	ld hl,.data
.next_phase	ld a,(hl)
	or a
	jr z,.end
	ld b,a	; b := width of trailing colour stripe
	inc hl
	ld a,(hl)	; a := starting x position
	inc hl
	ld c,(hl)	; c := loop counter: steps across the screen
	inc hl
	ld d,(hl)	; d := trailing colour
	inc hl
	ld e,(hl)	; e := leading colour
	inc hl

	; Draw the next frame of the splash animation, and test some RAM while we wait for the next frame.
	; We always test the same amount of RAM per frame. We *could* test more in later frames, once we get
	; past the 16K slow memory… but the difference would be marginal.
.phase_loop:
	halt	; Wait for next frame.
	call STRIPE_n
	
	push af	; NOW CHECK SOME RAM!
	exx
	ld a,(G.KEY_CHAR)
	or b
	ld b,a	; B = bitwise OR of recent keypresses.
	dec c
	inc c
	jr nz,.allowed_to_exit	; If we’ve already tested all memory, allow exit on keypress. Otherwise, test more memory.
	push bc
	ld bc,$9003	; Test 2.5 blocks of RAM per phase, (actually 0x290 = 656 bytes).
	jp RAM_TEST.do_test
.ram_test_return:	pop bc
	jr z,.continue_phase_loop ; Block of RAM passed, but we might not be finished, so continue with next phase.
	ld (G.RAMTOP),hl	; Test ‘failed’, but it might just have hit the start of ROM.
	inc hl	; Round up one byte.
	ld a,h
	sub 16*4	; Subtract 16 KB of ROM (in pages: 16 * 4 pages/KB).
	rrca	; Divide by 2
	rrca	; Divide by 2
	and %00111111	; Mask off two junk bits that we rotated in.
	cp MINIMUM_RAM	; Do we have at least the minimum RAM?
	jp c,RAM_TEST.BAD_RAM_SIZE	; Otherwise, show RAM failure message.
	ld (G.RAMSIZE_KB),a	; Store RAM size in KB, for display purposes.
	ld c,a	; Store RAM size in C, to record that we finished.
.allowed_to_exit:	inc b
	dec b
	jp nz,REPL
.continue_phase_loop:	exx
	pop af
	
	dec c
	jr nz,.phase_loop
	jr .next_phase

.data:
.cyan_cyan	=	%00101101
.cyan_blue	=	%00101001
.cyan_black	=	%00101000
.cyan_white	=	%00101111
.green_cyan	=	%00100101
.green_green	=	%00100100
.green_blue	=	%00100001
.yellow_yellow	=	%00110110
.yellow_green	=	%00110100
.red_red	=	%00010010
.red_yellow	=	%00010110
.white_white	=	%00111111
.white_red	=	%00111010

	; WIPE CYAN
	db 2, 0	; width, startx
	db 22	; iterations
	db .cyan_cyan, .cyan_black

	; WIPE GREEN
	db 2, 1	; width, startx
	db 21	; iterations
	db .green_green, .green_cyan

	; WIPE YELLOW
	db 4, 0	; width, startx
	db 11	; iterations
	db .yellow_yellow, .yellow_green

	; WIPE RED
	db 4, -1	; width, startx
	db 11	; iterations
	db .red_red, .red_yellow

	; WIPE WHITE
	db 4, -2	; width, startx
	db 11	; iterations
	db .white_white, .white_red

	; END
	DB 0
.end
	ld b,8	; We could save two bytes here by rearranging data a bit, and assigning B in the loop…
	ld c,0b00101000	; Cyan paper
.border_loop	halt	; Wait for next frame
	out ($fe), a	; set border, in spectrum from black to white. Makes transition softer & less jarring.
	xor c	; Add in paper colour.
	ld ($5aff), a	; bottom-right screen cell
	ld ($5adf), a	; (bottom-1)-right screen cell
	xor c	; Remove paper colour again.
	inc a
	djnz .border_loop

COPYRIGHT:
	ld de,COPYRIGHT_TEXT
	ld bc,(COPYRIGHT_TEXT.length << 8) | $7f
	ld a,%00111000	; Black ink on white paper
	ld hl,$1602	; x := 2 (half-cells), y := 22
	call RENDER_TEXT

.wait_for_key:	halt
	ld a,(G.KEY_CHAR)
	or a
	jp nz,REPL
	jr .wait_for_key

STRIPE_n:
	; Draw a steep diagonal section of rainbow down the screen, from given x position,
	; A: x-position (0–45); D: trailing attribute; E: leading attribute; B: number of trailing cells (1+)
	; B,C,D,E,H,L preserved; A incremented by B
	push hl
	push bc
	ld c,e
	call STRIPE
	ld c,d

.trail_loop:	dec a
	call STRIPE
	djnz .trail_loop
	pop bc
	pop hl
	add b
	add b
	ret

	; Draw a steep diagonal section of rainbow down the screen, from given x position,
	; A: x-position (0–45); C: attribute to paint
	; A,F,B,C,D,E preserved; H,L trashed
STRIPE:
	push af
	push bc
	push de
	ld de,32
	ld h,$58	; HL := attribute pointer
	ld l,a
	sub e
	jr nc,.stripe_rightoff	; Stripe position is beyond edge of screen
	ld a,l
	ld b,12
	cp b	; half of screen height
	jr nc,.loop	; pattern is uninterrupted, and does not cross off screen
.stripe_leftoff:
	inc a
.counter_is_in_a
	ld b,a
.loop:
	ld (hl),c
	add hl,de
	ld (hl),c
	add hl,de
	dec l
	djnz .loop
	pop de
	pop bc
	pop af
	ret
.stripe_rightoff:
	inc a	; a := distance past last column + 1
	add a	; a := double that (so row to start painting on)
	ld b,a	; b := same thing (but in B, to use as counter)
	ld a,32+12
	sub l	; a := height (in double cells) of stripe to paint
	ld l,31	; l := 31 (last column)
.drop_down:
	add hl,de
	djnz .drop_down
	jr .counter_is_in_a

COPYRIGHT_TEXT:
	db $a9,' 1982 Sinclair Research Ltd.'
.length	= $ - COPYRIGHT_TEXT

INITIAL_GLOBAL_VALUES:		; (Basically just the interrupt handler for now.)
.font:	dw FONT_LOOKUP
.SYNTAX_COLOURS:	dw DEFAULT_SYNTAX_COLOURS
.repdel:	db 35
.repper:	db 5
.i_handler:	jp DEFAULT_INTERRUPT_HANDLER
.length

	; Copies User Defined Graphics into RAM
	; We depend on the destination buffer fitting inside a 256-byte page, and do all-8-bit maths.
	; We also depend on ALL UDG template characters being 2 half-cells wide, 6 pixels tall and
	; 1 pixel of top leading.
	ASSERT (UDG_RAM_START & $ff) + UDG_RAM_LENGTH <= 256
	ASSERT FONT_DATA.udg_are_all_6px_high_with_1px_leading == 1
RESET_UDG:	push hl
	push de
	push bc

	ld de,FONT_DATA.udg_start
	ld hl,UDG_RAM_START
	ld c,16	; Count of UDG characters
	xor a	; Start with empty pixel line
.each_char:
	ld (hl),%1000000	; Transformed to 8-pixels-high and 0 leading
	inc l
	ld b,7	; 7 pixels tall INCLUDING leading

.pixel_data:	ld (hl),a
	inc l
	inc de	; First iteration, this skips past glyph header
	ld a,(de)
	djnz .pixel_data

	xor a
	ld (hl),a
	inc l
	dec c
	jr nz,.each_char

	pop bc
	pop de
	pop hl
	ret